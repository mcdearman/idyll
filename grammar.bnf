<item> ::= <expr> | <data>

<data> ::= "data" <ws>+ <ident> <ws>+ "=" <ws>+ (<recordDef> | <sumDef>)

<recordDef> ::= "{" <ws>* <ident> <ws>* ("," <ws>* <ident> <ws>*)* "}"

<sumDef> ::= <ident> (<ws>+ <variant>)* (<ws>+ "|" <ws>+ <ident> (<ws>+ <variant>)*)*

<variant> ::= <ident> | <recordDef>

<expr> ::= <or>

<or> ::= <and> (<ws>* "||" <ws>* <and>)*

<and> ::= <eq> (<ws>* "&&" <ws>* <eq>)*

<eq> ::= <cmp> (<ws>* ("=" | "!=") <ws>* <cmp>)?

<cmp> ::= <term> (<ws>* ("<" | ">" | "<=" | ">=") <ws>* <term>)*

<term> ::= <factor> (<ws>* ("+" | "-") <ws>* <factor>)*

<factor> ::= <power> (<ws>* ("*" | "/" | "%") <ws>* <power>)*

<power> ::= <unary> (<ws>* "^" <ws>* <unary>)*

<unary> ::= <apply> | ("-" | "!") <unary>

<apply> ::= <atom> (<ws>+ <atom>)*

<atom> ::= <ident> | <lit> | <if> | <let> | "(" <expr> ")"

<let> ::= "let" <ws>+ <ident>+ <ws>+ "=" <ws>+ <expr> (<ws>+ "in" <ws>+ <expr>)?

<if> ::= "if" <ws>+ <expr> <ws>+ "then" <ws>+ <expr> 
    (<ws>+ "elif" <ws>+ <expr> <ws>+ "then" <ws>+ <expr>)* (<ws>+ "else" <ws>+ <expr>)?

<lit> ::= <int> 
| <float> 
| <string> 
| <char> 
| <bool> 
| <lambda>

<lambda> ::= "\\" <ws>+ <ident>+ <ws>+ "->" <ws>+ <expr>

<string> ::= "\"" <ascii>* "\""

<char> ::= "'" <ascii> "'"

<ascii> ::= <letterOrDigit> | <ws>

<ident> ::= <letter> <letterOrDigit>*

<letterOrDigit> ::= <letter> | <digit>

<letter> ::= "_" | [a-z] | [A-Z]

<bool> ::= "true" | "false"

<float> ::= <int> "." <digit>+

<int> ::= "0" | <nonzero> <digit>*

<nonzero> ::= [1-9]

<digit> ::= "0" | <nonzero>

<ws> ::= " " | "\n" | "\t" | "\r"
