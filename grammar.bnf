<item> ::= <data> | <decl> | <expr>

<data> ::= "data" <ws>+ <ident> <ws>+ "=" <ws>+ "{" <ws>* <ident> <ws>* ("," <ws>* <ident> <ws>*)* "}"

<decl> ::= "let" (<ws>+ "rec")? <ws>+ <declBind> | <declFn>

<declBind> ::= <pattern> <ws>+ "=" <ws>+ <expr>

<declFn> ::= <ident> (<ws>+ <letPattern>)+ <ws>+ "=" <ws>+ <expr>

<expr> ::= <or>

<or> ::= <and> (<ws>* "||" <ws>* <and>)*

<and> ::= <eq> (<ws>* "&&" <ws>* <eq>)*

<eq> ::= <cmp> (<ws>* ("=" | "!=") <ws>* <cmp>)?

<cmp> ::= <term> (<ws>* ("<" | ">" | "<=" | ">=") <ws>* <term>)*

<term> ::= <factor> (<ws>* ("+" | "-") <ws>* <factor>)*

<factor> ::= <power> (<ws>* ("*" | "/" | "%") <ws>* <power>)*

<power> ::= <unary> (<ws>* "^" <ws>* <unary>)*

<unary> ::= <apply> | ("-" | "!") <unary>

<apply> ::= <atom> (<ws>+ <atom>)*

<atom> ::= <ident> | <lit> | <if> | <let> | <match> | "(" <expr> ")"

<let> ::= "let" (<ws>+ "rec")? <ws>+ <letBind> | <letFn>

<letBind> ::= <pattern> <ws>+ "=" <ws>+ <expr> <ws>+ "in" <ws>+ <expr>?

<letFn> ::= <ident> (<ws>+ <letPattern>)+ <ws>+ "=" <ws>+ <expr> <ws>+ "in" <ws>+ <expr>?

<if> ::= "if" <ws>+ <expr> <ws>+ "then" <ws>+ <expr> 
    (<ws>+ "elif" <ws>+ <expr> <ws>+ "then" <ws>+ <expr>)* (<ws>+ "else" <ws>+ <expr>)?

<match> ::= "match" <ws>+ <expr> <ws>+ "with" <ws>+ <fullPattern> <ws>+ "->" <ws>+ <expr> 
    (<ws>+ "|" <ws>+ <fullPattern> <ws>+ "->" <ws>+ <expr>)*

<fullPattern> ::= <ident> 
| <lit> 
| <fullListPattern> 
| <fullTuplePattern> 
| <fullTapPattern> 
| <fullRecordPattern> 
| <wildcard> 
| <unit>

<fullListPattern> ::= "[" <ws>* <pattern> (<ws>* "," <ws>* <pattern>)* <ws>* "]"

<fullTuplePattern> ::= "(" <ws>* <pattern> (<ws>* "," <ws>* <pattern>)* <ws>* ")"

<fullMapPattern> ::= "{" <ws>* <string> ":" <ws>* <pattern> (<ws>* "," <ws>* <string> ":" <ws>* <pattern>)* <ws>* "}"

<fullRecordPattern> ::= <ident> <ws>+ "{" <ws>* <ident> <ws>* ("=" <ws>* <pattern>)? <ws>* ("," <ws>* <ident> ("=" <ws>* <pattern>)? <ws>*)* "}"

<letPattern> ::= <ident>
| <letListPattern>
| <letTuplePattern>
| <letTapPattern>
| <wildcard>
| <unit>

<letRecordPattern> ::= <ident> <ws>+ "{" <ws>* <ident> <ws>* ("=" <ws>* <letPattern>)? <ws>* ("," <ws>* <ident> ("=" <ws>* <letPattern>)? <ws>*)* "}"

<letListPattern> ::= "[" <ws>* <letPattern> (<ws>* "," <ws>* <letPattern>)* <ws>* "]"

<letTuplePattern> ::= "(" <ws>* <letPattern> (<ws>* "," <ws>* <letPattern>)* <ws>* ")"

<letMapPattern> ::= "{" <ws>* <string> ":" <ws>* <letPattern> (<ws>* "," <ws>* <string> ":" <ws>* <letPattern>)* <ws>* "}"

<lit> ::= <int> 
| <float> 
| <string> 
| <char> 
| <bool> 
| <lambda>
| <list>
| <tuple>
| <map>

<lambda> ::= "\\" <ws>+ <ident>+ <ws>+ "->" <ws>+ <expr>

<list> ::= "[" <ws>* <expr> (<ws>* "," <ws>* <expr>)* <ws>* "]"

<tuple> ::= "(" <ws>* <expr> (<ws>* "," <ws>* <expr>)* <ws>* ")"

<map> ::= "{" <ws>* <string> ":" <ws>* <expr> (<ws>* "," <ws>* <string> ":" <ws>* <expr>)* <ws>* "}"

<string> ::= "\"" <ascii>* "\""

<char> ::= "'" <ascii> "'"

<ascii> ::= <letterOrDigit> | <ws>

<ident> ::= <letter> <letterOrDigit>*

<letterOrDigit> ::= <letter> | <digit>

<letter> ::= <wildcard> | [a-z] | [A-Z]

<bool> ::= "true" | "false"

<float> ::= <int> "." <digit>+

<int> ::= "0" | <nonzero> <digit>*

<nonzero> ::= [1-9]

<digit> ::= "0" | <nonzero>

<ws> ::= " " | "\n" | "\t" | "\r"

<wildcard> ::= "_"

<unit> ::= "()"