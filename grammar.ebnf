root = decl+

decl = let

let = "let" pat "=" expr

expr = or

or = and ("or" and)*

and = eq ("and" eq)*

eq = cmp (("=" | "!=") cmp)*

cmp = term ((">" | ">=" | "<" | "<=") term)*

term = factor (("+" | "-") factor)*

factor = pow (("*" | "/" | "%") pow)*

pow = unary ("^" unary)*

unary = apply | ("-" | "!") unary

apply = atom+

atom = ident | lit | if | lambda | letExpr | "(" expr ")"

if = "if" expr "then" expr "else" expr

lambda = "\\" pat+ "->" expr

letExpr = "let" pat "=" expr "in" expr

match = "match" expr "with" "{" matchCase+ "}"

matchCase = pat "->" expr

pat = wildcard
    | lit
    | ident+
    | "(" pat ")"
    | "[" pat ("," pat)* "]"
    | "{" pat ("," pat)* "}"

ident = [a-zA-Z_][a-zA-Z0-9_]*

lit = num | str | bool

num = int ("/" int)?

int = 0 | "-"? [1-9][0-9]*

str = "(\\.|[^"\\])*"

bool = "true" | "false"

wildcard = "_"


